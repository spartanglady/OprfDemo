plugins {
    id 'java-library'
    id 'maven-publish'
}

dependencies {
    // Bouncy Castle for cryptographic operations
    api 'org.bouncycastle:bcprov-jdk18on:1.78.1'

    // Testing
    testImplementation platform('org.junit:junit-bom:5.10.2')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'oprf-core'
            from components.java
            pom {
                name = 'OPRF Core Library'
                description = 'Java implementation of OPRF (Oblivious Pseudorandom Function) per RFC 9497'
                url = 'https://github.com/spartanglady/OprfDemo'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                scm {
                    url = 'https://github.com/spartanglady/OprfDemo'
                    connection = 'scm:git:git://github.com/spartanglady/OprfDemo.git'
                    developerConnection = 'scm:git:ssh://git@github.com/spartanglady/OprfDemo.git'
                }
            }
        }
    }
    repositories {
        maven {
            name = 'GitHubPackages'
            url = uri('https://maven.pkg.github.com/spartanglady/OprfDemo')
            credentials {
                username = project.findProperty('gpr.user') ?: System.getenv('GITHUB_ACTOR')
                password = project.findProperty('gpr.key') ?: System.getenv('GITHUB_TOKEN')
            }
        }
    }
}
